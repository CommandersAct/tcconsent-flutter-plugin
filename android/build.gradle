group 'com.tagcommander.tc_mobile_flutter_plugin'

def getPubspecVersion() {

    def pubspec = new File(projectDir.parentFile, "pubspec.yaml")
    if (!pubspec.exists()) {
        throw new GradleException("pubspec.yaml not found at ${pubspec.absolutePath}")
    }
    def text = pubspec.getText("UTF-8")
    // Matches e.g. 1.2.3, 1.2.3+4, 1.2.3-dev.1, etc.
    def m = (text =~ /(?m)^\s*version:\s*([0-9]+\.[0-9]+\.[0-9]+(?:[+-][0-9A-Za-z\.\-]+)?)\s*$/)
    if (!m.find()) {
        throw new GradleException("version field not found in pubspec.yaml")
    }
    return m.group(1)
}

ext {
    PLUGIN_VERSION = getPubspecVersion()
}

version PLUGIN_VERSION

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.7.3'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'

android {
    compileSdk 35

    namespace 'com.tagcommander.lib.tcconsent_plugin'
    defaultConfig {
        minSdkVersion 21
        multiDexEnabled true
    }
}

dependencies{
    //Commanders Act SDK dependencies

    api 'com.tagcommander.lib:core:5.4.8'
    api 'com.tagcommander.lib:Consent:5.3.8'
    api 'com.tagcommander.lib:TCIAB:5.1.0'
}
